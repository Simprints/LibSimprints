apply plugin: 'maven-publish'
apply plugin: 'signing'

// Set up Sonatype repository
nexusPublishing {
    repositories {
        sonatype {
            packageGroup = publishedGroupId
            stagingProfileId = ossStagingProfileId
            username = ossrhUsername
            password = ossrhPassword
        }
    }
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = publishedGroupId
version = libraryVersion

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId publishedGroupId
                artifactId artifact
                version libraryVersion

                if (project.plugins.findPlugin("com.android.library")) {
                    from(components.findByName("release"))
                } else {
                    from(components.java)
                }

                artifact androidSourcesJar

                pom {
                    name = artifact
                    description = libraryDescription
                    url = siteUrl
                    licenses {
                        license {
                            name = licenseName
                            url = licenseUrl
                        }
                    }
                    developers {
                        developer {
                            id = developerId
                            name = developerName
                            email = developerEmail
                        }
                    }
                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}