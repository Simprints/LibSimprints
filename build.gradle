buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:8.10.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:2.1.21"
        classpath "io.github.gradle-nexus:publish-plugin:2.0.0"
    }
}

apply plugin: "com.android.library"
apply plugin: "org.jetbrains.kotlin.android"

repositories {
    google()
    mavenCentral()
}

project.version = "2025.2.0"
ext {
    VERSION_CODE = 202502000
}

android {
    namespace = "com.simprints.libsimprints"
    compileSdk = 35

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 35

        versionName project.version
        // Version code should match the name as it is used in SID
        // to determine the most appropriate response data format
        versionCode project.property("VERSION_CODE")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

            buildConfigField("Integer", "LIBRARY_VERSION_CODE", "${project.property("VERSION_CODE")}")
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            buildConfigField("String", "LIBRARY_PACKAGE_VERSION", "\"${project.version}\"")
            buildConfigField("Integer", "LIBRARY_VERSION_CODE", "${project.property("VERSION_CODE")}")
        }
        staging {
            initWith release
        }
        endToEndTesting {
            initWith debug
            testCoverageEnabled false
        }
        profiling {
            initWith debug
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.1.20"
    implementation 'androidx.activity:activity-ktx:1.10.1'

    testImplementation "junit:junit:4.13.2"
    testImplementation "androidx.test.ext:junit:1.2.1"
    testImplementation "androidx.test:core:1.6.1"
    testImplementation "org.robolectric:robolectric:4.14.1"
}

apply plugin: "io.github.gradle-nexus.publish-plugin"
apply from: ".github/publish/publishConfig.gradle"
apply from: ".github/publish/publish-mavencentral.gradle"
